# @(#)73	1.4  src/tcpip/usr/sbin/gated/sigconv.awk, tcprouting, tcpip411, GOLD410 12/6/93 18:00:33
#
#   COMPONENT_NAME: tcprouting
#
#   FUNCTIONS: none
#
#   ORIGINS: 27,36
#
#
#   (C) COPYRIGHT International Business Machines Corp. 1993
#   All Rights Reserved
#   Licensed Materials - Property of IBM
#   US Government Users Restricted Rights - Use, duplication or
#   disclosure restricted by GSA ADP Schedule Contract with IBM Corp.
#
#*******************************************************************************
#
#       The origin of this software will be explicitly stated as developed 
#       by Cornell University and its collaborators.
#
#       You agree to use reasonable efforts to provide Cornell University with 
#       all modifications, enhancements and documentation you make to GateD, 
#       including documentation related to your distribution of GateD.  Cornell 
#       University shall have the rights to redistribute the modifications,   
#       enhancements, and documentation without accounting to you.
#
#*******************************************************************************

BEGIN {
  nsig = 0;
  last_sig = 0;
}

/^#define/ {
  sig = $3 + 0;
  if (sig != 0) {
    if ($2 == "NSIG" || $2 == "SIGMAX") {
      nsig = sig;
    } else if ( (substr($2,1,3) == "SIG") && (substr($2,4,1) != "_") ) {
      if ((sig <= nsig) && (sig != last_sig)) {
        last_sig = sig;
        signals[sig] = $2;
      }
    }
  }
}

END {
  print "/* This file was automatically generated by the awk script \"sigconv.awk\".*/\n"
  printf "static bits signal_names[NSIG+1] = {\t/* %s */\n", nsig
  for (sig = 1; sig <= nsig; sig++) {
    if (length(signals[sig])) {
      printf "#ifdef\t%s\n", signals[sig]
      printf "\t{ %s,\t\"%s\" },\t/* %s */\n", \
       signals[sig], substr(signals[sig], 4), sig
      printf "#else\t%s\n", signals[sig]
    }
    printf "\t{ 0,\t\tNULL },\t\t/* %s */\n", sig
    if (length(signals[sig])) {
      printf "#endif\t%s\n", signals[sig]
    }
  }
  print "\t{ 0, \t\tNULL }\t\t/* EOL */\n};"
}
